5 'ART WALK SUGOROKU -NDA-
10 '8/30-9/2/19
15 DIM SP(9)
25 T=0:R=0:P1=1:P2=1:O=16
35 CLS:VIEW
40 LOCATE 2,20:PRINT"ART WALK SUGOROKU 2019 "
45 PLAY "T1O2DE#FEGR5#FEAR5AR5D"
50 LOCATE 2,22:PRINT"     PRESS [START]   "
55 BTN=STRIG(0):IF BTN<>1 THEN 55
60 DEF SPRITE 0,(0,1,0,0,0)="0123"
65 DEF SPRITE 1,(2,1,0,0,0)=CHR$(20)+CHR$(21)+CHR$(22)+CHR$(23)
75 GOSUB800:SPRITE 0,SP(0),SP(1):SPRITE 1,SP(0)+O,SP(1)
85 LOCATE 2,22:PRINT"                   ":SPRITE ON
100 GOSUB 800
105 IF T=0 THEN LOCATE 1,18:PRINT"P1";CHR$(207):LOCATE 23,18:PRINT"   "
106 IF T=1 THEN LOCATE 23,18:PRINT CHR$(207);"P2":LOCATE 1,18:PRINT"   "
110 LOCATE 2,20:PRINT"PRESS B BUTTON TO ROLL!"
125 BTN=STRIG(0):IF BTN<>4 THEN 125
130 LOCATE 2,20:PRINT"     ROLLING DIE...    ":GOSUB 900
135 I=R+1:I=SP(I):IF I=0 THEN 145
140 LOCATE 2,20:PRINT"YOU ROLLED";R;"! GO TO";I;"!":PAUSE 100:GOTO 150
145 PLAY"O1B#AA":LOCATE 2,20:PRINT"CONTEMPLATE FOR 1 TURN!":PAUSE 150:GOTO 185
150 IF T=0 THEN P1=I:O=0
151 IF T=1 THEN P2=I:O=16
155 GOSUB 800:SPRITE T,SP(0)+O,SP(1):PLAY"O2#FR5AB"
160 W=LEN(N$)
165 FOR Y=1 TO W:LOCATE 27,Y-1:PRINT MID$(N$,Y,1):NEXT
185 IF I=11 THEN GOTO 700
190 T=T+1:IF T>1 THEN T=0
195 GOTO 100
700 LOCATE 2,20:PRINT"   PLAYER ";T+1;" WINS!!!    ":PLAY"O2BABO3#CR5DE#F":PAUSE 200
705 FOR Y=0 TO 20:LOCATE 27,Y:PRINT" "
710 SPRITE OFF:GOTO 25
800 RESTORE 1001
803 IF T=0 THEN L=P1
804 IF T=1 THEN L=P2
805 FOR X=1 TO L
810 FOR Y=0 TO 7
820 READ SP(Y)
830 NEXT
835 READ N$
840 NEXT
850 RETURN
900 FOR Y=0 TO RND(3)
905 LOCATE 19,18:PRINT " ";
910 FOR X=0 TO 5:R=RND(6)+1
915 LOCATE X+13,18:PRINT" ":LOCATE X+13,18:PRINT R
920 PLAY "O1D#":NEXT:NEXT
930 RETURN
1001 DATA 184,143,5,9,2,0,4,7,FURIDASHI! GO ART!
1002 DATA 128,143,0,8,4,0,1,6,STILL LIFE W/ VASE
1003 DATA 72,143,0,8,10,0,1,0,MINIMALIST OBJECTS
1004 DATA 24,143,5,8,7,0,0,0,ORANGE COMPOSITION
1005 DATA 184,95,0,10,9,0,1,6,AERIAL DRONESCAPES
1006 DATA 128,95,1,0,0,0,2,7,ROTTEN APPLE STAND
1007 DATA 72,95,11,5,6,0,2,3,BLUE PEAK AT NIGHT
1008 DATA 24,95,0,6,2,0,3,9,ABSTRACT STUDY NO3
1009 DATA 184,47,7,0,4,0,3,0,INKWELL   FRAGMENT
1010 DATA 24,47,9,0,4,0,8,11,REFLECTING MORTARS
1011 DATA 72,47,0,0,0,0,0,0,AGARI! GIFT SHOP!!
